buildscript {
    ext {
        kotlinVersion = '1.2.71'
        springBootVersion = '2.1.0.RELEASE'
        domaVersion = '2.19.3'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'kotlin-kapt'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'jp.takeda'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

repositories {
    mavenCentral()
}

// JavaクラスとSQLファイルの出力先ディレクトリを同じにする
processResources.destinationDir = compileJava.destinationDir
// コンパイルより前にSQLファイルを出力先ディレクトリにコピーするために依存関係を逆転する
compileKotlin.dependsOn processResources

apply plugin: 'idea'
idea.module.inheritOutputDirs = true

// SQLファイルなどリソースファイルの出力先ディレクトリをkaptに伝える
kapt {
    arguments {
        arg("doma.resources.dir", processResources.destinationDir)
    }
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('com.fasterxml.jackson.module:jackson-module-kotlin')
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    implementation("org.jetbrains.kotlin:kotlin-reflect")
    implementation("org.seasar.doma.boot:doma-spring-boot-starter:1.1.1")
    implementation("org.seasar.doma:doma:${domaVersion}")
    implementation("io.springfox:springfox-swagger2:2.9.2")
    implementation("io.springfox:springfox-data-rest:2.9.2")
    implementation("io.springfox:springfox-swagger-ui:2.9.2")
    runtimeOnly('com.h2database:h2')
    kapt("org.seasar.doma:doma:${domaVersion}")
    testImplementation('org.springframework.boot:spring-boot-starter-test')
}
